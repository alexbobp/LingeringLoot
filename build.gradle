// For those who want the bleeding edge
buildscript {
    ext.kotlin_version = '1.0.2'
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        mavenCentral()
    }
    dependencies {
        classpath 'net.minecraftforge.gradle:ForgeGradle:2.2-SNAPSHOT'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath 'com.github.jengelman.gradle.plugins:shadow:1.2.4'
    }
}
apply plugin: 'kotlin'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'com.github.johnrengelman.shadow'

version = "2.6_experimental"
group = "lingerloot" // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = "LingeringLoot-1.11.2"

minecraft {
    version = "1.11.2-13.20.1.2386"
    runDir = "run"
    
    // the mappings can be changed at any time, and must be in the following format.
    // snapshot_YYYYMMDD   snapshot are built nightly.
    // stable_#            stables are built at the discretion of the MCP team.
    // Use non-default mappings at your own risk. they may not allways work.
    // simply re-run your setup task after changing the mappings to update your workspace.
    mappings = "stable_32"
//    mappings = "snapshot_20170624"
    makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
}

repositories {
    jcenter()
    maven {
        url = 'http://unascribed.com/maven/releases'
    }
    maven {
        url "http://maven.shadowfacts.net/"
    }
}

dependencies {
    compile group: "net.shadowfacts", name: "Forgelin", version: "1.5.0"
    compile 'com.unascribed:lambdanetwork:1.0.0'
    shadow 'com.unascribed:lambdanetwork:1.0.0'
}

shadowJar {
    relocate 'org.jetbrains.annotations', 'lingerlootkot.jetbrains.annotations'
    relocate 'com.unascribed.lambdanetwork', "lingerlootkot.unascribed.lambdanetwork"
    classifier ''
    configurations = [project.configurations.shadow]
}

reobf {
    shadowJar { mappingType = 'SEARGE' }
}

tasks.build.dependsOn reobfShadowJar

processResources {
    // this will ensure that this task is redone when the versions change.
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    // replace stuff in mcmod.info, nothing else
    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'
                
        // replace version and mcversion
        expand 'version':project.version, 'mcversion':project.minecraft.version
    }
        
    // copy everything else, thats not the mcmod.info
    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }

    rename '(.+_at.cfg)', 'META-INF/$1'
}

sourceSets {
    main.java.srcDirs += 'src/main/kotlin'
}

jar {
    manifest {
        attributes 'FMLAT': 'lingeringloot_at.cfg'
    }
}